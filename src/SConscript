Import('env')

env.Append(CPPPATH = ['include'])

# Set up the environment for compiling the libraries
libenv = env.Clone()
libenv.Append(CPPPATH = ['lib'])

# Prepare the libobj and shlibobj object files
libsrc = ['lib/tapcfg.c','lib/taplog.c']
libobj = libenv.Object(['lib/tapcfg.c','lib/taplog.c'])
if libenv['STATIC_AND_SHARED_OBJECTS_ARE_THE_SAME']:
	shlibobj = libobj
else:
	shlibobj = libenv.SharedObject(['lib/tapcfg.c','lib/taplog.c'])

# Compile the static library into lib directory and shared library
# for the bindings
libenv.Library('lib/tapcfg', libobj, install=False)
libenv.SharedLibrary('bindings/tapcfg', shlibobj, install=False)

# Compile the .Net bindings
asm = env.CLILibrary('bindings/Ethernet', ['bindings/EthernetDevice.cs','bindings/EthernetFrame.cs'])
prog = env.CLIProgram('bindings/TAPCfgTest', ['bindings/TAPCfgTest.cs'], CILLIBS=asm)

# Set up the environment for compiling the demo tests
demoenv = env.Clone()
conf = Configure(demoenv)
conf.CheckLib('pthread')
demoenv = conf.Finish()
demoenv.Append(LIBPATH = ['lib'])
demoenv.Prepend(LIBS = ['tapcfg'])

# Compile the demo test executables
listener = demoenv.Object('demos/listener.c')
demoenv.Program('demos/test_client', ['demos/test_client.c'], install=False)
demoenv.Program('demos/test_server', [listener,'demos/test_server.c'], install=False)
demoenv.Program('demos/test_forwarder', [listener,'demos/test_forwarder.c'], install=False)

