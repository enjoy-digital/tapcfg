Import('env')

env.Append(CPPPATH = ['include'])

libenv = env.Clone()
conf = Configure(libenv)
conf.CheckLib('ws2_32')
libenv = conf.Finish()
libenv.Append(CPPPATH = ['lib'])

libsrc = ['lib/tapcfg.c','lib/taplog.c']
libenv.Library('lib/tapcfg', libsrc)
libenv.SharedLibrary('bindings/tapcfg', libsrc)

asm = env.CLILibrary('bindings/Ethernet', ['bindings/EthernetDevice.cs','bindings/EthernetFrame.cs'])
prog = env.CLIProgram('bindings/TAPCfgTest', ['bindings/TAPCfgTest.cs'], CILLIBS=asm)

demoenv = env.Clone()
conf = Configure(demoenv)
conf.CheckLib('pthread')
conf.CheckLib('ws2_32')
demoenv = conf.Finish()
demoenv.Append(LIBPATH = ['lib'])
demoenv.Prepend(LIBS = ['tapcfg'])

listener = demoenv.Object('demos/listener.c')
demoenv.Program('demos/test_client', ['demos/test_client.c'], install=False)
demoenv.Program('demos/test_server', [listener,'demos/test_server.c'], install=False)
demoenv.Program('demos/test_forwarder', [listener,'demos/test_forwarder.c'], install=False)

