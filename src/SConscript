Import('env')

env.Append(CPPPATH = ['include'])

# Set up the environment for compiling the libraries
libenv = env.Clone()
libenv.Append(CPPPATH = ['lib'])

# Prepare the libobj and shlibobj object files
libsrc = ['lib/tapcfg.c','lib/taplog.c']
libobj = libenv.Object(['lib/tapcfg.c','lib/taplog.c'])
if libenv['STATIC_AND_SHARED_OBJECTS_ARE_THE_SAME']:
	shlibobj = libobj
else:
	shlibobj = libenv.SharedObject(['lib/tapcfg.c','lib/taplog.c'])

# Compile the static library into lib directory and shared library
# for the bindings
libenv.Library('lib/tapcfg', libobj, install=False)
libenv.SharedLibrary('../build/tapcfg', shlibobj, install=False)

# Compile the .Net bindings
asm = env.CLILibrary('../build/TAP', ['bindings/EthernetDevice.cs','bindings/EthernetFrame.cs','bindings/UTF8Marshaler.cs'])
prog = env.CLIProgram('../build/TAPCfgTest', ['demos/TAPCfgTest.cs','demos/IPv4.cs','demos/IPv6.cs','demos/ProtocolType.cs','demos/NeighborDiscovery.cs','demos/VirtualRouter.cs'], CILLIBS=asm)

# Set up the environment for compiling the demo tests
appenv = env.Clone()
conf = Configure(appenv)
conf.CheckLib('pthread')
appenv = conf.Finish()
appenv.Append(LIBPATH = ['lib'])
appenv.Prepend(LIBS = ['tapcfg'])

# Compile the daemon for testing
appenv.Program('../build/tapserver', ['daemon/tapserver.c','daemon/main.c'], install=False)

